; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=PeerBlock
AppVerName=PeerBlock 1.0
AppPublisher=PeerBlock Project
AppPublisherURL=http://sites.google.com/site/peerblock/
AppSupportURL=http://sites.google.com/site/peerblock/
AppUpdatesURL=http://sites.google.com/site/peerblock/
DefaultDirName={pf}\PeerBlock
DefaultGroupName=PeerBlock
SourceDir=../
LicenseFile=setup/license.txt
InfoBeforeFile=setup/readme.rtf
OutputDir=setup
OutputBaseFilename=setup
Compression=lzma/ultra
SolidCompression=yes
MinVersion=4.1,5.0
AppVersion=2.1.0.2
VersionInfoVersion=2.1.0.2
AppMutex=Global\PeerBlock
ArchitecturesAllowed=x86 x64
ArchitecturesInstallIn64BitMode=x64

[Tasks]
Name: "startupicon"; Description: "&Start with Windows"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quickicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; Win2k files
Source: "win32\release\pg2.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: Is2k
Source: "win32\release\pgfilter.sys"; DestDir: "{app}"; Flags: ignoreversion; Check: Is2k
; WinXP x64 files
Source: "x64\release\pg2.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: IsXP64
Source: "x64\release\pgfilter.sys"; DestDir: "{app}"; Flags: ignoreversion; Check: IsXP64
; Vista files
Source: "win32\release (vista)\pg2.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: IsVista
Source: "win32\release (vista)\pgfilter.sys"; DestDir: "{app}"; Flags: ignoreversion; Check: IsVista
; Vista x64 files
Source: "x64\release (vista)\pg2.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: IsVista64
Source: "x64\release (vista)\pgfilter.sys"; DestDir: "{app}"; Flags: ignoreversion; Check: IsVista64
; Common files
Source: "license.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "readme.txt"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[INI]
Filename: "{app}\peerblock.url"; Section: "InternetShortcut"; Key: "URL"; String: "http://sites.google.com/site/peerblock/"

[Icons]
Name: "{group}\PeerBlock"; Filename: "{app}\pg2.exe"
Name: "{group}\{cm:ProgramOnTheWeb,PeerBlock}"; Filename: "{app}\peerblock.url"
Name: "{userdesktop}\PeerBlock"; Filename: "{app}\pg2.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\PeerBlock"; Filename: "{app}\pg2.exe"; Tasks: quickicon

[Registry]
Root: HKCU; Subkey: "Software\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "PeerBlock"; ValueData: "{app}\pg2.exe"; Flags: uninsdeletevalue; Tasks: startupicon

[Run]
Filename: "{app}\pg2.exe"; Description: "{cm:LaunchProgram,PeerBlock}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{app}\peerblock.url"

[Code]
function Is9x: Boolean;
begin
  Result := not UsingWinNT;
end;

function Is2k: Boolean;
var
  ver: TWindowsVersion;
begin
  GetWindowsVersionEx(ver);
  Result := UsingWinNT and (ver.Major < 6) and not Is64BitInstallMode;
end;

function IsXP64: Boolean;
var
  ver: TWindowsVersion;
begin
  GetWindowsVersionEx(ver);
  Result := UsingWinNT and (ver.Major < 6) and Is64BitInstallMode;
end;

function IsVista: Boolean;
var
  ver: TWindowsVersion;
begin
  GetWindowsVersionEx(ver);
  Result := UsingWinNT and (ver.Major >= 6) and not Is64BitInstallMode;
end;

function IsVista64: Boolean;
var
  ver: TWindowsVersion;
begin
  GetWindowsVersionEx(ver);
  Result := UsingWinNT and (ver.Major >= 6) and Is64BitInstallMode;
end;

